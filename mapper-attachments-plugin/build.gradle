import de.undercouch.gradle.tasks.download.Download

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath group: 'de.undercouch', name: 'gradle-download-task', version: '3.3.0'

        classpath group: 'com.github.jengelman.gradle.plugins', name: 'shadow', version: '2.0.2'
    }
}

group 'org.ozoneplatform'
version '5.4.3'

apply plugin: 'java'

apply plugin: 'de.undercouch.download'
apply plugin: 'com.github.johnrengelman.shadow'

ext {
    tikaVersion = '1.14'
    pdfboxVersion = '2.0.3'
    commonsLoggingVersion = '1.1.3'
    commonsCodecVersion = '1.10'
    bouncycastleVersion = '1.55'
    poiVersion = '3.15'
    mime4jVersion = '0.7.2'
}

configurations {
    shaded
}

dependencies {
    // mandatory for tika
    compile "org.apache.tika:tika-core:$tikaVersion"
    compile "org.apache.tika:tika-parsers:$tikaVersion"
    compile 'commons-io:commons-io:2.4'

    // character set detection
    compile 'com.googlecode.juniversalchardet:juniversalchardet:1.0.3'

    // external parser libraries
    // HTML
    compile 'org.ccil.cowan.tagsoup:tagsoup:1.2.1'

    // Adobe PDF
    compile "org.apache.pdfbox:pdfbox:$pdfboxVersion"
    compile "org.apache.pdfbox:fontbox:$pdfboxVersion"
    compile "org.apache.pdfbox:jempbox:1.8.12"
    compile "commons-logging:commons-logging:$commonsLoggingVersion"
    compile "org.bouncycastle:bcmail-jdk15on:$bouncycastleVersion"
    compile "org.bouncycastle:bcprov-jdk15on:$bouncycastleVersion"
    compile "org.bouncycastle:bcpkix-jdk15on:$bouncycastleVersion"

    // OpenOffice
    compile "org.apache.poi:poi-ooxml:$poiVersion"
    compile "org.apache.poi:poi:$poiVersion"
    compile "org.apache.poi:poi-ooxml-schemas:$poiVersion"
    compile "commons-codec:commons-codec:$commonsCodecVersion"
    compile 'org.apache.xmlbeans:xmlbeans:2.6.0'

    // MS Office
    compile "org.apache.poi:poi-scratchpad:$poiVersion"
    
    // Apple iWork
    compile 'org.apache.commons:commons-compress:1.10'

    // Outlook documents
    compile "org.apache.james:apache-mime4j-core:$mime4jVersion"
    compile "org.apache.james:apache-mime4j-dom:$mime4jVersion"

    shaded fileTree(dir: "libs/elasticsearch", include: 'mapper-attachments-*.jar')
}

shadowJar {
    configurations = [project.configurations.shaded]
}

task downloadAttachmentsPlugin(type: Download) {
    src "https://artifacts.elastic.co/downloads/elasticsearch-plugins/mapper-attachments/mapper-attachments-${elasticsearchVersion}.zip"
    dest "libs/mapper-attachments-${elasticsearchVersion}.zip"

    overwrite true
    onlyIfNewer true
}

task downloadAndUnzipAttachmentsPlugin(dependsOn: downloadAttachmentsPlugin) {
    doLast {
        copy {
            from zipTree(downloadAttachmentsPlugin.dest)
            into 'libs/'
        }
    }
}

compileJava {
    dependsOn downloadAndUnzipAttachmentsPlugin
}

task cleanLibsDir(type: Delete) {
    delete fileTree(dir: 'libs')
}

clean.doLast {
    cleanLibsDir.execute()
}
